plugins {
    id "org.sonarqube" version "1.2"
    id 'me.champeau.gradle.jmh' version '0.2.0'
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'npetzall.xpath'
version '0.1.0'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.13'
    testCompile 'org.testng:testng:6.9.9'
    testCompile 'org.assertj:assertj-core:3.1.0'
    testRuntime 'org.apache.logging.log4j:log4j-core:2.5'
    testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
    testRuntime 'org.codehaus.sonar-plugins.java:sonar-jacoco-listeners:3.2'
}

test {
    useTestNG() {
        listeners << 'org.sonar.java.jacoco.TestNGListener'
    }
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

test.finalizedBy jacocoTestReport

jmh {
    jmhVersion = '1.11.3'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['bintray']
    pkg {
        repo = 'maven'
        name = 'simple-xpath'
        desc = 'simple-xpath library using stream,event and callbacks. For use with npetzall projects'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/npetzall/simple-xpath'
        issueTrackerUrl = 'https://github.com/npetzall/simple-xpath/issues'
        vcsUrl = 'https://github.com/npetzall/simple-xpath.git'
        githubRepo = 'npetzall/simple-xpath'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
